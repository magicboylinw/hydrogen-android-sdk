apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

def sdkVersionCode = 42
def sdkVersionName = "1.2.1"

android {
    compileSdkVersion 28

    // 需要 java 8 的支持
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    defaultConfig {
        // autobahn-android 需要 >= 24
        minSdkVersion minSdkVer
        targetSdkVersion targetSdkVer
        versionCode sdkVersionCode
        versionName sdkVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            consumerProguardFiles 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            consumerProguardFiles 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

}

dependencies {

    testImplementation 'junit:junit:4.12'

    androidTestImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'

    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation("com.squareup.okhttp3:okhttp:3.13.1")
    implementation("com.squareup.retrofit2:retrofit:2.5.0")
    api("com.squareup.retrofit2:converter-gson:2.5.0")

    api fileTree(dir: 'libs', include: ['*.aar', '*.jar'])
    api 'com.tencent.mm.opensdk:wechat-sdk-android-without-mta:5.3.1'
    api 'com.sina.weibo.sdk:core:4.3.7:openDefaultRelease@aar'
    api 'com.meizu.flyme.internet:push-internal:3.9.7'
    api 'com.huawei.hms:push:5.0.2.300'
    api("com.google.guava:guava:29.0-android")

    implementation 'io.reactivex.rxjava2:rxjava:2.2.12'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

    api('com.minapp.android.autobahn:autobahn-android:20.7.9') {
        // 跟依赖的 rxjava 版本冲突了
        exclude group: "io.reactivex.rxjava2", module:"rxjava"
    }
}

// 把 sdk 发布到 bintray：./gradlew :sdk:clean :sdk:bintrayUpload
def bintrayConfig = project.file('../bintray.properties')
if (bintrayConfig != null && bintrayConfig.exists()) {
    Properties prop = new Properties()
    prop.load( bintrayConfig.newDataInputStream() )

    apply plugin: 'com.novoda.bintray-release'

    publish {
        bintrayUser = prop.getProperty("bintrayUser")
        bintrayKey = prop.getProperty("bintrayKey")
        userOrg = ""
        repoName = "minapp-android-sdk"

        // 项目的坐标
        groupId = "com.minapp.android"
        artifactId = "sdk"
        publishVersion = sdkVersionName

        desc = "minapp-android-sdk"
        uploadName = "minapp-android-sdk"
        website = "https://github.com/ifanrx/hydrogen-android-sdk"
        dryRun = false
    }
}